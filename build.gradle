plugins {
    id 'application'
    id 'java'
    id 'com.github.spotbugs'
    id 'com.google.protobuf'
    id 'idea'

}

group = 'lt.pow.nukagit'
version = '1.0-SNAPSHOT'
mainClassName = 'lt.pow.nukagit.Main'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit.archive:_'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:_'
    implementation 'org.apache.sshd:sshd-core:_'
    implementation 'org.apache.sshd:sshd-git:_'

    implementation 'com.google.protobuf:protobuf-java:_'
    implementation 'io.grpc:grpc-stub:_'
    implementation 'io.grpc:grpc-protobuf:_'
    implementation 'javax.annotation:javax.annotation-api:1.3.1'

    implementation 'org.slf4j:slf4j-api:_'
    implementation 'ch.qos.logback:logback-classic:_'
    implementation 'ch.qos.logback:logback-core:_'

    implementation 'com.google.guava:guava:_'

    testImplementation platform('org.junit:junit-bom:_')
    testImplementation 'org.junit.jupiter:junit-jupiter:_'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:_'
    testImplementation 'org.eclipse.jgit:org.eclipse.jgit.junit:_'
    testImplementation 'com.google.truth:truth:_'
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = 'com.google.protobuf:protoc:3.6.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.15.1'
        }
    }
    generateProtoTasks {
        ofSourceSet('main').configureEach {
            plugins {
                grpc {}
            }
        }
    }
}

spotbugs {
    excludeFilter = file('spotbugs-exclude.xml')
}

test {
    useJUnitPlatform()
}

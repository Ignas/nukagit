plugins {
    id 'application'
    id 'java'
    id 'groovy'
    id 'com.github.spotbugs'
    id 'idea'
    id 'com.ryandens.javaagent-application' version "0.4.2"
}

group = 'lt.pow.nukagit'
version = '0.1-SNAPSHOT'
mainClassName = 'lt.pow.nukagit.Main'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':proto')

    // Jgit and SSHD
    implementation 'org.eclipse.jgit:org.eclipse.jgit.archive:_'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:_'
    implementation 'org.apache.sshd:sshd-core:_'
    implementation 'org.apache.sshd:sshd-git:_'

    // Dagger
    implementation 'com.google.dagger:dagger:_'
    annotationProcessor 'com.google.dagger:dagger-compiler:_'
    implementation 'javax.annotation:javax.annotation-api:_'

    // Metrics
    implementation 'io.micrometer:micrometer-core:_'

    // Grpc
    implementation 'com.google.protobuf:protobuf-java:_'
    implementation 'io.grpc:grpc-stub:_'
    implementation 'io.grpc:grpc-protobuf:_'
    implementation 'io.grpc:grpc-netty:_'
    implementation 'io.grpc:grpc-services:_'

    // Logging
    implementation 'org.slf4j:slf4j-api:_'
    implementation 'ch.qos.logback:logback-classic:_'
    implementation 'ch.qos.logback:logback-core:_'

    // Database
    implementation 'com.zaxxer:HikariCP:_'
    implementation 'org.jdbi:jdbi:_'
    implementation 'org.jdbi:jdbi3-core:_'
    implementation 'org.jdbi:jdbi3-sqlobject:_'
    implementation 'mysql:mysql-connector-java:_'
    implementation 'org.flywaydb:flyway-core:_'
    implementation 'org.flywaydb:flyway-mysql:_'

    // Minio
    implementation 'io.minio:minio:_'

    // Utilities
    implementation 'com.google.guava:guava:_'
    implementation 'org.immutables:value:_'
    annotationProcessor "org.immutables:value:_"

    // Config
    implementation 'com.github.gestalt-config:gestalt-core:_'
    implementation 'com.github.gestalt-config:gestalt-yaml:_'

    // Testing
    testImplementation platform('org.junit:junit-bom:_')
    testImplementation 'org.junit.jupiter:junit-jupiter:_'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:_'
    testImplementation 'org.eclipse.jgit:org.eclipse.jgit.junit:_'
    testImplementation 'com.google.truth:truth:_'

    // Command line parsing
    implementation 'info.picocli:picocli:_'

    // Spock
    testImplementation platform('org.apache.groovy:groovy-bom:_')
    testImplementation platform("org.spockframework:spock-bom:_")
    testImplementation 'org.apache.groovy:groovy:_'
    testImplementation "org.spockframework:spock-core:_"
    testImplementation "org.hamcrest:hamcrest-core:_"
    testImplementation "org.testcontainers:spock:_"
    testImplementation 'org.testcontainers:mysql:_'
    testRuntimeOnly 'net.bytebuddy:byte-buddy:_'
    testRuntimeOnly "org.objenesis:objenesis:_"

    javaagent("io.opentelemetry.javaagent:opentelemetry-javaagent:_")
}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

spotbugs {
    excludeFilter = file("${rootDir}/spotbugs-exclude.xml")
}

run {
    systemProperty "otel.metrics.exporter", "prometheus"
    systemProperty "otel.exporter.prometheus.port", "9091"
    systemProperty "otel.traces.exporter", "none"
    systemProperty "otel.service.name", "nukagit"
    // systemProperty "otel.javaagent.debug", "true"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    def homeDirectory = System.getProperty("user.home")
    def colimaSocketFilename = "${homeDirectory}/.colima/default/docker.sock"
    def colimaSocket = new File(colimaSocketFilename)
    if (colimaSocket.exists()) {
        environment("DOCKER_HOST", "unix://${colimaSocketFilename}")
        environment("TESTCONTAINERS_DOCKER_SOCKET_OVERRIDE", "/var/run/docker.sock")
        environment("TESTCONTAINERS_RYUK_DISABLED", "true")
    }
}
